apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.getkeepsafe.dexcount'

apply from: "$project.rootDir/tools/versioning.gradle"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.yichiuan.moedict"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode generateVersionCode(rootProject.ext.minSdkVersion, 0, versionMajor, versionMinor, versionPatch)
        versionName generateVersionName(versionMajor, versionMinor, versionPatch)

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', 'VOICE_BASE_URL', '"https://203146b5091e8f0aafda-15d41c68795720c6e932125f5ace0c70.ssl.cf1.rackcdn.com/"'
    }
    buildTypes {
        debug {
            minifyEnabled false
            versionNameSuffix '-debug'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets.test.resources.srcDirs += ["src/main"]
}

dexcount {
    format = "yaml"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    implementation "com.android.support:appcompat-v7:${libs.supportVersion}"
    implementation "com.android.support:recyclerview-v7:${libs.supportVersion}"

    //RxJava
    implementation "io.reactivex.rxjava2:rxjava:${libs.rxjava}"
    implementation "io.reactivex.rxjava2:rxandroid:${libs.rxandroid}"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7:${libs.rxbinding}"
    implementation "com.jakewharton.rx2:replaying-share:${libs.replaying_share}"

    // Dagger DI
    implementation "com.google.dagger:dagger:${libs.dagger}"
    kapt "com.google.dagger:dagger-compiler:${libs.dagger}"

    implementation "com.google.dagger:dagger-android-support:${libs.dagger}"
    kapt "com.google.dagger:dagger-android-processor:${libs.dagger}"

    // For Lifecycle and ViewModel
    implementation "android.arch.lifecycle:extensions:${libs.arch_components}"
    implementation "android.arch.lifecycle:viewmodel:${libs.arch_components}"
    annotationProcessor "android.arch.lifecycle:compiler:${libs.arch_components}"

    // For Debug
    implementation "com.jakewharton.timber:timber:${libs.timber}"

    debugImplementation "com.squareup.leakcanary:leakcanary-android:${libs.leakcanary}"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${libs.leakcanary}"

    // For Testing
    testImplementation "junit:junit:${libs.junit}"
    testImplementation "org.mockito:mockito-core:${libs.mockito}"
    androidTestImplementation "org.mockito:mockito-android:${libs.mockito}"

    androidTestImplementation "com.android.support.test:runner:${libs.test_runner}"
    androidTestImplementation "com.android.support.test:rules:${libs.test_rules}"

    androidTestImplementation("com.android.support.test.espresso:espresso-core:${libs.espresso}", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
